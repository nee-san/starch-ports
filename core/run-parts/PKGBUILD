# $Id: PKGBUILD 162908 2012-07-03 17:24:27Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=run-parts
pkgver=4.3.2
pkgrel=1
pkgdesc='run scripts or programs in a directory'
arch=('i686' 'x86_64')
url='http://packages.qa.debian.org/d/debianutils.html'
license=('GPL')
makedepends=()
source=("ftp://ftp.archlinux.org/other/run-parts/debianutils_${pkgver}.tar.gz")
sha256sums=('0062d774306a6acc34d3b855a5c4eeb845653b0cd34dbb5c13aa00b4ecb8af22')

build() {
	cd $srcdir/debianutils-$pkgver
	./configure --prefix=/usr
	make run-parts
}

package() {
	cd $srcdir/debianutils-$pkgver
	install -D -m755 run-parts $pkgdir/usr/bin/run-parts
	install -D -m644 run-parts.8 $pkgdir/usr/share/man/man8/run-parts.8
	for l in po4a/*/run-parts.8; do
		install -D -m644  ${l} $pkgdir/usr/share/man/${l:5:2}/man8/run-parts.8
	done
}
