# $Id: PKGBUILD 163854 2012-07-21 11:22:49Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=bash
_basever=3.0
_patchlevel=0 #prepare for some patches
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnu.org/software/bash/bash.html"
groups=('base')
backup=(etc/bash.bash{rc,_logout} etc/skel/.bash{rc,_profile,_logout})
depends=()
makedepends=()
provides=('sh')
install=bash.install
source=(http://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz{,.sig}
        dot.bashrc
        dot.bash_profile
        dot.bash_logout
        system.bashrc
        system.bash_logout)
if [ $_patchlevel -gt 000 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi

build() {
  cd ${srcdir}/${pkgname}-$_basever
  #for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
  #  msg "applying patch bash${_basever//./}-$(printf "%03d" $p)"
  #  patch -Np0 -i $srcdir/bash${_basever//./}-$(printf "%03d" $p)
  #done

  _bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin\"\'
               -DSTANDARD_UTILS_PATH=\'\"/usr/bin:/bin:/usr/sbin:/sbin\"\'
               -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
               -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\')
  export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

  bash_cv_getenv_redef=0 \
  ./configure --prefix=/usr --with-curses --disable-nls --disable-readline \
    --enable-minimal-config --enable-static-link \
    --without-bash-malloc --without-libintl-prefix \
    --bindir=/bin --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

check() {
  cd ${srcdir}/${pkgname}-$_basever
  #make check
  true
}

package() {
  cd ${srcdir}/${pkgname}-$_basever
  make DESTDIR=${pkgdir} install

  # for now, bash is our default /bin/sh
  cd ${pkgdir}/bin
  ln -s bash sh

  install -dm755 ${pkgdir}/etc/skel/
  
  # system-wide configuration files
  install -m644 ${srcdir}/system.bashrc ${pkgdir}/etc/bash.bashrc
  install -m644 ${srcdir}/system.bash_logout ${pkgdir}/etc/bash.bash_logout
  
  # user configuration file skeletons
  install -m644 ${srcdir}/dot.bashrc ${pkgdir}/etc/skel/.bashrc
  install -m644 ${srcdir}/dot.bash_profile ${pkgdir}/etc/skel/.bash_profile
  install -m644 ${srcdir}/dot.bash_logout ${pkgdir}/etc/skel/.bash_logout
}

sha1sums=('3acf1ff4910d4bc863620c7533cbf4858370017b'
          'd0a1e373959e5868719c0c979b23ae90de88ce08'
          'a573cf76b3cdc72928add2585f68ca97b54b6d33'
          '672c3c0c28c3642a5cff0e4093ff98359c747ff0'
          '9fd0cfda5b85651169f8761a834941b1f6f53952'
          '895ffd3f5c729271859a659c4ca17aefc3fda927'
          '0bc4dc72ef407eefe27b6ba5b384ccddb3e0852b')
