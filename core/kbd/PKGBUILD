# $Id: PKGBUILD 163716 2012-07-18 02:17:05Z dreisner $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kbd
pkgver=1.15.3
pkgrel=3
pkgdesc="Keytable files and keyboard utilities"
arch=('i686' 'x86_64')
url="ftp://ftp.altlinux.org/pub/people/legion/kbd/"
license=('GPL')
makedepends=()
source=(ftp://ftp.altlinux.org/pub/people/legion/kbd/${pkgname}-${pkgver}.tar.gz
        fix-es.po.patch
        kbd_no_error.diff yylval.diff include.diff)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ../fix-es.po.patch
  cat ../*.diff | patch -Np 1
  # rename keymap files with the same names
  # this is needed because when only name of keymap is specified
  # loadkeys loads the first keymap it can find, which is bad (see FS#13837)
  # this should be removed when upstream adopts the change
  mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
  mv data/keymaps/i386/olpc/es{,-olpc}.map
  mv data/keymaps/i386/olpc/pt{,-olpc}.map
  mv data/keymaps/i386/dvorak/no{,-dvorak}.map
  mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
  mv data/keymaps/i386/colemak/{en-latin9,colemak}.map


  ./configure --prefix=/usr --datadir=/usr/share/kbd --mandir=/usr/share/man
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes DESTDIR=${pkgdir} install
}

sha1sums=('1da0c02cb802d2e68b34b1ba68839929b4255cde'
          'b638e6242705e3018c1a950a78cddbb2c7b16c30'
          '23f24de3c626c92572b3eae49f794cf579c6635a'
          '3094d5e5c231978a82820c4a91c3b12edeaad395'
          'f23f288375b660ea60c3a13d6f7048f167c6cf50')
