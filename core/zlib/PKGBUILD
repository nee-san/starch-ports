# $Id: PKGBUILD 158552 2012-05-04 22:14:53Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=zlib
pkgver=1.2.7
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('i686' 'x86_64')
license=('custom')
url="http://www.zlib.net/"
depends=()
source=("http://zlib.net/current/zlib-${pkgver}.tar.gz")
md5sums=('60df6a37c56e7c1366cca812414f7b85')

build() {
	cd ${srcdir}/zlib-$pkgver
	./configure --prefix=/usr
	make

	sed -n '/Copyright/,$p;/@/q' <zlib.h >LICENSE
}

check() {
	cd ${srcdir}/zlib-$pkgver
	# make test
	true
}

package() {
	cd ${srcdir}/zlib-$pkgver
	make install DESTDIR=${pkgdir}
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE
}
